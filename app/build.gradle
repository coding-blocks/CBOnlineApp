plugins {
    id "com.diffplug.gradle.spotless" version "3.23.1"
}

apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

def YOUTUBE_KEY = System.getenv('YOUTUBE_KEY') ?: "AIzaSyAqdhonCxTsQ5oQ-tyNaSgDJWjEM7UaEt4"
def CLIENT_ID = System.getenv('CLIENT_ID') ?: "5633768694"
def REDIRECT_URI = System.getenv('REDIRECT_URI') ?: "app://online.codingblocks.com"
def OAUTH_URL = System.getenv('OAUTH_URL') ?: "https://account.codingblocks.com/oauth/authorize"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.codingblocks.cbonlineapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 100040802
        // 1 (versioning scheme)
        // 00 (major version)
        // 04 (minor version)
        // 06 (patch version)
        // 02 spare for betas/RCs
        versionName "0.4.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled false
        manifestPlaceholders = [
                onesignal_app_id               : '01d75aac-5123-4145-b655-ba8d27c5dc13',
                onesignal_google_project_number: 'REMOTE'
        ]
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix '-debug-version'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "YOUTUBE_KEY", '"' + YOUTUBE_KEY + '"'
            buildConfigField "String", "CLIENT_ID", '"' + CLIENT_ID + '"'
            buildConfigField "String", "REDIRECT_URI", '"' + REDIRECT_URI + '"'
            buildConfigField "String", "OAUTH_URL", '"' + OAUTH_URL + '"'
            applicationIdSuffix = 'debug'

        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "YOUTUBE_KEY", '"' + YOUTUBE_KEY + '"'
            buildConfigField "String", "CLIENT_ID", '"' + CLIENT_ID + '"'
            buildConfigField "String", "REDIRECT_URI", '"' + REDIRECT_URI + '"'
            buildConfigField "String", "OAUTH_URL", '"' + OAUTH_URL + '"'

        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

ext {
    ankoVersion = '0.10.6'
    room_version = "2.1.0-beta01"
    koin_version = "1.0.2"
    ankoLibs = [
            // commons
            'commons',
            // layouts
            'sdk25', 'appcompat-v7', 'support-v4', 'recyclerview-v7', 'constraint-layout', 'cardview-v7',
            // coroutines
            'sdk25-coroutines', 'appcompat-v7-coroutines', 'coroutines',
            'design',
            // 'percent',
    ]
}
spotless {
    kotlin {
        ktlint()
        target '**/*.kt'
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.material:material:1.0.0'
    // Android Support
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'com.google.android.play:core:1.6.0'
    // 3rd party libs
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.ethanhua:skeleton:1.1.2'
    implementation 'io.supercharge:shimmerlayout:2.1.0'
    implementation 'io.github.inflationx:calligraphy3:3.0.0'
    implementation 'io.github.inflationx:viewpump:1.0.0'
    implementation 'es.voghdev.pdfviewpager:library:1.0.3'
    implementation 'com.github.tiagohm.MarkdownView:library:0.19.0'
    implementation 'com.github.jasminb:jsonapi-converter:0.9'
    implementation 'com.onesignal:OneSignal:3.10.9'

    // Image view libraries
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'com.caverock:androidsvg-aar:1.4'

    //database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"


    //VideoCipher library
    implementation 'com.vdocipher.aegis:vdocipher-android:1.0.0-beta6'

    // Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"


    //Routers
    implementation 'com.github.campusappcn.AndRouter:router:1.2.10'
    kapt 'com.github.campusappcn.AndRouter:compiler:1.2.10'

    //Anko
    ankoLibs.each { name ->
        implementation "org.jetbrains.anko:anko-$name:$ankoVersion"
    }
    //Firebase
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-messaging:19.0.1'
    implementation 'com.google.firebase:firebase-invites:16.1.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-inappmessaging-display:18.0.1'


    implementation project(':onlineapi')
    implementation files('libs/YouTubeAndroidPlayerApi.jar')

    // Test Deps
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
}
